package analizadorlexico;
import java_cup.runtime.*;
import analizadorlexico.AST.*;
import analizadorlexico.AST.Declaration.*;
import analizadorlexico.AST.Statement.*;
import analizadorlexico.AST.Primary.*;
import analizadorlexico.AST.Expression.*;
import analizadorlexico.TypeCheck.*;
import java.util.List;

parser code {:
    public InitProcedure FINALOBJECT;
    public String TEST = "";
    public void syntax_error(Symbol cur_token) {
        String Found = getTokenName(cur_token.sym);
        if (cur_token.value != null){
            Found = "\""+cur_token.value.toString()+"\"";
        }
        System.err.print("Syntax Error:");
        System.err.print(" line:" + cur_token.left);
        System.err.print(" column:" + cur_token.right);
        System.err.println();
        expected_token();
        System.err.print(" Found Token: " + Found);
        System.err.println();
        System.err.println();
        //System.err.println(this.expected_token_ids());
    }


    public void expected_token(String token){
      System.err.print("Expected Token " + token);
      //System.err.println();
    };

    public String getTokenName(int id){
        String value = sym.terminalNames[id];
        if (id == sym.RELATION_OPERATOR){
          value ="<,>,>=,<=";
        }else if (id == sym.END_INSTRUCTION){
          value = ";";
        }else if (id == sym.CONDITION_ELEMENT){
          value = "(and, or)";
        }else if (id == sym.COMMA){
          value = ",";
        }else if (id == sym.LITERAL_BOOLEAN){
          value = "(true, false)";
        }else if (id == sym.LITERAL_INT){
          value = "integer";
        }else if (id == sym.LITERAL_STRING){
          value = "String";
        }else if (id == sym.OPEN_PARENTHESIS){
          value = "(";
        }else if (id == sym.CLOSE_PARENTHESIS){
          value = ")";
        }
        value = "\""+value+"\"";
        return value;
    }
    public void expected_token(){
      List<Integer> expected = this.expected_token_ids();
      if (expected.size() <= 0){
        expected = this.expected_token_ids();
      }
      String expectedVals = "";
      for (int i = 0 ; i < expected.size(); i++){
        int id = expected.get(i);
        if (id == 1){
          continue;
        }
        String value = getTokenName(id);
        if (i == expected.size() - 1){
          expectedVals += value;
        }else{
          expectedVals += value + " or ";
        }

      }
      expected_token(expectedVals);
    };

:}

/*TERMINAL (FLEX)*/
terminal PROCEDURE, IS, BEGIN, END, NEW_LINE, WITH, OPEN_PARENTHESIS;
terminal CLOSE_PARENTHESIS;
terminal END_INSTRUCTION, LITERAL_CHAR;
terminal IF,ELSE,ELSEIF,THEN,FOR,IN,OUT,LOOP,WHILE,ASIGNATION;
terminal DECLARATION, EXIT, WHEN, COMMA;
terminal CONSTANT,FUNCTION,PACKAGE,BODY,RETURN,NULL,CASE,CASEASIG;
terminal REVERSE,OTHERS,TO;

terminal String ID,PUT,LITERAL_STRING,SUM_OPERATOR, MULT_OPERATOR,RELATION_OPERATOR,CONDITION_ELEMENT;
terminal Type TYPE;
terminal String LITERAL_BOOLEAN;
terminal String LITERAL_INT;

/*NON TERMINAL*/
non terminal InitProcedure procedure;
non terminal Statement asignation,statement,if,if_a,case,while,for,statement_call;
non terminal CaseBody case_a;
non terminal Primary function_call,primary,function_call_statement;
non terminal ListPrimary list_primary, parameters;
non terminal Declaration declaration,in_out_declaration,simple_declaration,declaration_call;
non terminal Declaration asignation_declaration;
non terminal Expression expression,simple_expression,mathematical_expression,mult_operation,parenthesis_operation;
non terminal Expression condition_expression,relation_expression;
non terminal Condition condition;
non terminal Boolean constant;
non terminal ListDeclarationParameter list_declaration_parameters;
non terminal ListDeclarationParameter input_parameters;
non terminal String finalprocedure;


/*PRECEDENCE*/
precedence left CONDITION_ELEMENT;
precedence left RELATION_OPERATOR;
precedence left SUM_OPERATOR;
precedence left MULT_OPERATOR;
precedence left procedure;
precedence left finalprocedure;

/* GRAMMARS */
procedure ::= PROCEDURE ID:id IS declaration:d BEGIN statement:s finalprocedure:eid
              {: 
                  RESULT = new InitProcedure(id,eid,d,s);
                  parser.FINALOBJECT = RESULT;
              :}
              | error ID:id IS declaration:d BEGIN statement:s finalprocedure:eid
              {: 
                  RESULT = new InitProcedure(id,eid,d,s);
                  parser.FINALOBJECT = RESULT;
              :}
               | PROCEDURE error IS declaration:d BEGIN statement:s finalprocedure:eid
              {: 
                  RESULT = new InitProcedure(null,eid,d,s);
                  parser.FINALOBJECT = RESULT;
              :}
                | PROCEDURE ID:id error declaration:d BEGIN statement:s finalprocedure:eid
              {: 
                  RESULT = new InitProcedure(id,eid,d,s);
                  parser.FINALOBJECT = RESULT;
              :}
              | PROCEDURE ID:id IS error BEGIN statement:s finalprocedure:eid
              {: 
                  RESULT = new InitProcedure(id,eid,null,s); 
              :}
              | PROCEDURE error;
finalprocedure ::= END ID:id END_INSTRUCTION
                    {: RESULT = id; :}
                   | END error END_INSTRUCTION
                   {:  :}
                   | END error
                   {:  :};
declaration ::=   declaration_call:d declaration:sd
            {: RESULT = new SequenceDeclaration(d,sd); :}
            |
            {: 
            RESULT = new EmptyDeclaration(); :}
            | error END_INSTRUCTION
            {: RESULT = new EmptyDeclaration(); :};

declaration_call ::= simple_declaration:s END_INSTRUCTION
          {: RESULT = s; :}
          | PROCEDURE ID:i input_parameters:ip IS declaration:d BEGIN statement:s END ID:i2 END_INSTRUCTION
          {: 
            RESULT = new ProcedureDeclaration(i,i2,ip,d,s); 
          :}
          | FUNCTION ID:i input_parameters:ip RETURN TYPE:t IS declaration:d BEGIN statement:s END ID:i2 END_INSTRUCTION
          {: 
            RESULT = new FunctionDeclaration(i,i2,ip,d,s,t); 
          :}
          | asignation_declaration:ad END_INSTRUCTION
          {: RESULT = ad; :};
input_parameters ::= OPEN_PARENTHESIS list_declaration_parameters:lp CLOSE_PARENTHESIS
                  {: RESULT = lp; :}
                  |
                  {: RESULT = new ListDeclarationParameter(); :};

list_declaration_parameters ::= simple_declaration:d END_INSTRUCTION list_declaration_parameters:lp
                      {:RESULT = new ListDeclarationParameter(d,lp); :}
                      | in_out_declaration:d END_INSTRUCTION list_declaration_parameters:lp
                      {:RESULT = new ListDeclarationParameter(d,lp); :}
                      | simple_declaration:d
                      {:RESULT = new ListDeclarationParameter(d,null); :}
                      | in_out_declaration:d
                      {:RESULT = new ListDeclarationParameter(d,null); :};

in_out_declaration ::= ID:i DECLARATION IN TYPE:t
                  {: RESULT = new InOutDeclaration(i,t,"IN"); :}
                  | ID:i DECLARATION OUT TYPE:t
                  {: RESULT = new InOutDeclaration(i,t,"OUT"); :}
                  | ID:i DECLARATION IN OUT TYPE:t
                  {: 
                    RESULT = new InOutDeclaration(i,t,"IN OUT"); 
                  :};
simple_declaration ::= ID:id DECLARATION constant:c TYPE:t
                    {: 
                      RESULT = new SimpleDeclaration(id,t,c); 
                    :}
                    | ID:i COMMA simple_declaration:sd
                    {: 
                      RESULT = new SimpleDeclaration(i,sd); 
                    :};
asignation_declaration ::= ID:id DECLARATION constant:c TYPE:t ASIGNATION expression:e
                          {: Declaration tmp = new SimpleDeclaration(id,t,c);
                             RESULT = new AsignationDeclaration(tmp,e);
                          :};
constant ::= CONSTANT
          {: RESULT = true; :}
          |
          {:RESULT = false; :};
statement ::= statement_call:s statement:sq 
          {: RESULT = new SequenceStatement(s,sq); :}
          |
          {: RESULT = new EmptyStatement(); :}
          | error statement;

statement_call ::= asignation:a END_INSTRUCTION
          {: RESULT = a; :}
          |   NEW_LINE END_INSTRUCTION
          {: RESULT = new NewLineStatement(); :}
          |   if:i
          {: RESULT = i; :}
          |   case:c
          {: RESULT = c; :}
          |   while:w
          {: RESULT = w; :}
          |   for:f
          {: RESULT = f; :}
          |   RETURN expression:e END_INSTRUCTION
          {: RESULT = new ReturnStatement(e); :}
          |   function_call_statement:fs
          {: RESULT = new FunctionCallStatement(fs); :}
          | error END_INSTRUCTION
          | ID error END_INSTRUCTION
          {:  :}
          ;
function_call_statement ::= function_call:fc END_INSTRUCTION
          {: RESULT = fc; :};
condition ::= expression:e
          {: RESULT = new Condition(e); :};
simple_expression ::= ;

mathematical_expression ::= mathematical_expression:me SUM_OPERATOR:so mult_operation:me2
          {: RESULT = new AddExpression(me,me2,so); :}
          | mult_operation:mo
          {: RESULT = mo; :};
mult_operation ::= mult_operation:mo MULT_OPERATOR:mult parenthesis_operation:po
          {: RESULT = new MultExpression(mo,po,mult); :}
          | parenthesis_operation:po
          {: RESULT = po; :}
          ;
parenthesis_operation ::= OPEN_PARENTHESIS mathematical_expression:me CLOSE_PARENTHESIS
          {: RESULT = me; :}
          | LITERAL_INT:i
          {: RESULT = new PrimaryExpression(new LiteralInt(Integer.parseInt(i))); :}
          | ID:i
          {: RESULT = new PrimaryExpression(new ID(i)); :}
          | function_call:fc
          {:RESULT = new PrimaryExpression(fc); :};

expression ::= relation_expression:e
          {: RESULT = e; :}
          | condition_expression:e
          {: RESULT = e; :}
          | mathematical_expression:e
          {: RESULT = e; :}
          | LITERAL_BOOLEAN:e
          {: RESULT = new PrimaryExpression(new LiteralBoolean(Boolean.parseBoolean(e))); :}
          | LITERAL_STRING:e
          {: RESULT = new PrimaryExpression(new LiteralString(e)); :};


relation_expression ::= expression:e RELATION_OPERATOR:r expression:e1
                        {: RESULT = new RelationExpression(e,e1,r); :};
condition_expression ::= expression:e CONDITION_ELEMENT:c expression:e1
                    {: RESULT = new ConditionExpression(e,e1,c); :}
                    |   OPEN_PARENTHESIS condition_expression:c CLOSE_PARENTHESIS
                    {:RESULT = c;:};

primary  ::= LITERAL_BOOLEAN:e
          {:RESULT = new LiteralBoolean(Boolean.parseBoolean(e)); :}
          |   LITERAL_INT:i
          {:RESULT = new LiteralInt(Integer.parseInt(i)); :}
          |   LITERAL_STRING:S
          {:RESULT = new LiteralString(S);:}
          |   ID:i
          {: RESULT = new ID(i); :}
          |   function_call:fc
          {: RESULT = fc; :};
asignation   ::= ID:id ASIGNATION expression:e
            {: RESULT = new AsignationStatement(id,e); :};
                //parser.expected_token(":=");:};
list_primary ::= primary:p COMMA list_primary:lp
            {: RESULT = new ListPrimary(p,lp); :}
            |primary:p error list_primary:lp
            {: RESULT = new ListPrimary(p,lp); :}
            | primary:p
            {: RESULT = new ListPrimary(p); :};
parameters ::= list_primary:lp
          {: RESULT = lp; :}
          |
          {:RESULT = new ListPrimary(); :};

if  ::= IF condition:c THEN statement:s if_a:ef END IF END_INSTRUCTION
        {: RESULT = new IfStatement(c,s,ef); :}
        | IF condition:c THEN statement:s if_a:ef END IF error:e
        | IF error END IF END_INSTRUCTION
        {://parser.expected_token("; IF");:};
if_a ::= ELSEIF condition:c THEN statement:s if_a:ef
        {: RESULT = new IfStatement(c,s,ef); :}
        | ELSE statement:s
        {: RESULT = new IfStatement(s); :}
        |
        {: RESULT = new EmptyStatement(); :} ;

case  ::= CASE primary:p IS case_a:cb END CASE END_INSTRUCTION
          {: RESULT = new CaseStatement(p,cb); :}
          | CASE primary:p IS case_a:cb END CASE error
          | CASE error CASE END_INSTRUCTION
          {://parser.expected_token("; CASE");:};
case_a ::= WHEN primary:p CASEASIG statement:s case_a:cb
          {: RESULT = new CaseBody(p,s,cb); :}
          |
          {:RESULT = new CaseBody(); :} ;

while ::= WHILE condition:c LOOP statement:s END LOOP END_INSTRUCTION
        {: RESULT = new WhileStatement(c,s); :}
        | WHILE condition:c LOOP statement:s END LOOP error
        | WHILE error LOOP END_INSTRUCTION
        {://parser.expected_token("; WHILE");:};

for   ::= FOR asignation:a TO expression:e statement:s END FOR END_INSTRUCTION
        {: RESULT = new ForStatement(a,s,e); :}
        | FOR asignation:a TO expression:e statement:s END FOR error
        | FOR error FOR END_INSTRUCTION
        {://parser.expected_token("; FOR");:};


function_call ::= ID:id OPEN_PARENTHESIS parameters:p CLOSE_PARENTHESIS
            {: RESULT = new FunctionCall(id,p); :}
              |   PUT:id OPEN_PARENTHESIS parameters:p CLOSE_PARENTHESIS
              {: RESULT = new FunctionCall(id,p); :} 
              | PUT error;
